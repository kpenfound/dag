type Yarn {
  """
  The source directory for the project.
  """
  pub source: Directory! @defaultPath(path: "/")

  """
  The Node image to use
  """
  pub nodeImage: Container! = container.from("node:25-alpine")

  """
  Build the project
  """
  pub build: Changeset! {
    base.withExec(["yarn", "run", "build"]).
    directory("/src").
    changes(source)
  }

  """
  Test the project
  """
  pub test(workspace: String! = ""): Void @check {
    let cmd = ["yarn", "test"]
    if(workspace != "") {
      cmd = ["yarn", "workspace", workspace, "test"]
    }
    base
    .withExec(cmd).
    sync

    null
  }

  """
  Lint the project
  """
  pub lint: Void @check {
    base.withExec(["yarn", "run", "lint"]).
    sync

    null
  }

  let base: Container! {
    nodeImage.
      withMountedCache("/root/.npm", cacheVolume("node-modules")).
      withDirectory("/src", source).
      withWorkdir("/src").
      withEnvVariable("CI", "true").
      withExec(["yarn", "install"])
  }
}
